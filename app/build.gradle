plugins {
    id 'com.android.application'
}

android {
    namespace "com.project.laundryappui"
    compileSdkVersion 34
    buildToolsVersion "34.0.0"

    defaultConfig {
        applicationId "com.project.laundryappui"
        minSdkVersion 22
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "SUPABASE_URL", "\"${project.findProperty('SUPABASE_URL') ?: ''}\""
        buildConfigField "String", "SUPABASE_ANON_KEY", "\"${project.findProperty('SUPABASE_ANON_KEY') ?: ''}\""
        buildConfigField "String", "WA_REMINDER_API_URL", "\"${project.findProperty('WA_REMINDER_API_URL') ?: ''}\""
        buildConfigField "String", "WA_REMINDER_API_KEY", "\"${project.findProperty('WA_REMINDER_API_KEY') ?: ''}\""
        buildConfigField "String", "OWNER_EMAIL", "\"${project.findProperty('OWNER_EMAIL') ?: ''}\""
    }

    signingConfigs {
        release {
            // Provide these in ~/.gradle/gradle.properties or CI secret env
            def storeFileProp = project.findProperty('KEYSTORE_FILE') ?: ''
            if (storeFileProp) {
                storeFile file(storeFileProp)
            }
            storePassword project.findProperty('KEYSTORE_PASSWORD') ?: ''
            keyAlias project.findProperty('KEY_ALIAS') ?: ''
            keyPassword project.findProperty('KEY_PASSWORD') ?: ''
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    buildFeatures {
        buildConfig true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    //Extended Library
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    // fixed artifact name - use dotsindicator from Maven Central
    implementation 'com.tbuonomo:dotsindicator:4.2'
    implementation 'com.journeyapps:zxing-android-embedded:4.3.0'
}
